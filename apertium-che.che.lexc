! Morphological Transducer for Chechen

Multichar_Symbols

! Part of speech categories
! Noun
%<n%>   ! Noun
%<cl_v%>  ! v.class
%<cl_j%>  ! j.class
%<cl_j2%> ! j.class II
%<cl_d%>  ! d.class
%<cl_b%>  ! b.class
%<cl_b2%> ! b.class II

! Number feature
%<pl%>  ! Plural
%<sg%>  ! Singular


! Case
%<abs%> ! Absolutive
%<erg%> ! Ergative
%<gen%> ! Gentivie
%<dat%> ! Dative
%<all%> ! Allative
%<ins%> ! Instrumental
%<lat%> ! Lative
%<cmp%> ! Comparative
%<ine%> ! Inessive
%<abl%> ! Ablative

! Proper Noun
%<np%>   ! Proper Noun
%<fam%>  ! Family name

! Pronoun
%<prn%>   ! Pronoun
%<pers%>   ! Personal
%<p1%>   ! 1st-person
%<p2%>   ! 2nd-person
%<p3%>   ! 3rd-person
%<inc%>   ! Inclusive
%<exc%>   ! Exclusive
%<dem%>   ! Demonstrative
%<prx%>   ! Proximal
%<dst%>   ! Distal
%<itg%>   ! Interrogative

! Verb
%<v%>   ! Verb
%<tv%>  ! Transitive
%<iv%>  ! Intransitive
%<inf%> ! Infinite form
%<pres%> ! Present
%<past%> ! past
%<fut%>  ! Future
%<ptn%> ! Potential Future
%<imf%> ! Imperfective
%<pf%> ! Perfective
%<prog%> ! Progressive
%<rec%> ! Recent
%<rem%> ! Remote
%<imp%> ! Imperative
%<plt%> ! Polite Imperative
%<imd%> ! Immediate Imperative
%<tsk%> ! Tasked Imperative
%<ctg%> ! Categorical imperative
%<caus%> ! Causative
%<wit%> ! Witnessed
%<uwit%> ! Unwitnessed
%<dnm%> ! Verbal denominal
%<rcp%> ! Relative Clausal Participle

!Postposition
%<post%> ! Postposition

!Conjunction
%<conj%> !Conjunction

!Adjective
%<adj%> ! Adjective
%<cls%> ! Class Adjective
%<comp%> ! Comparative Adjective
%<ext%> ! Extended Comparative
%<sub%> ! Substantivized Adjective
%<idp%> ! Independent
%<dp%> ! Dependent

! Adverb
%<adv%> ! Adverb
%<tmp%> ! Time
%<loc%> ! Place
%<man%> ! Manner
%<rea%> ! Reason
%<quan%> ! Quantifying
%<des%> ! Descriptive

! Numeral
%<num%> ! Numeral
%<car%> ! Cardinal
%<ord%> ! Ordinal

%<unk%> ! Unknow


! Other symbols
%>      ! Morpheme boundary
%{a%}
%{i%}
%{i1%}
%{h%}
%{H%}
%{e%}
%{y%}
%{g%}
%{ee%}
%{г%}
%{x%}

%{b%}
%{b1%}
%{b2%}
%{YY%}
%{A%}
%{A1%}
%{E%}
%{EE%}
%{O%}
%{I%}
%{Y%}
%{AA%}


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

NounRoot ;
PropNounRoot ;
VerbRoot ;
Conjunction ;
ProNoun ;
AdjRoot ;

LEXICON NounVc !Absolutive forms aka. citation forms

%<n%>%<cl_v%>: RegNounUninfl ;

LEXICON NounJc

%<n%>%<cl_j%>: RegNounUninfl ;

LEXICON NounJ2c

%<n%>%<cl_j2%>: RegNounUninfl ;

LEXICON NounDc

%<n%>%<cl_d%>: RegNounUninfl ;

LEXICON NounBc

%<n%>%<cl_b%>: RegNounUninfl ;

LEXICON NounB2c

%<n%>%<cl_b2%>:  RegNounUninfl ;

LEXICON NounUc

%<n%>%<cl_u%>: RegNounUninfl ;


LEXICON NounDc-Aug-anim ! Augment base (different from abs forms)

%<n%>%<cl_d%>: RegNounInfl ;
%<n%>%<cl_d%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-anim;

LEXICON NounDc-Aug-inan ! Augment base (different from abs forms)

%<n%>%<cl_d%>: RegNounInfl ;
%<n%>%<cl_d%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-inan;

!LEXICON NounBc-Aug-anim

!%<n%>%<cl_b%>: RegNounInfl ;
!%<n%>%<cl_b%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-anim;

LEXICON NounBc-Aug-inan

%<n%>%<cl_b%>: RegNounInfl ;
%<n%>%<cl_b%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-inan;

!LEXICON NounB2c-Aug-anim

!%<n%>%<cl_b2%>:  RegNounInfl ;
!%<n%>%<cl_b2%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-anim;

LEXICON NounB2c-Aug-inan

%<n%>%<cl_b2%>:  RegNounInfl ;
%<n%>%<cl_b2%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-inan;


LEXICON NounVc-Aug-anim

%<n%>%<cl_v%>: RegNounInfl ;
%<n%>%<cl_v%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-anim;

LEXICON NounVc-Aug-inan

%<n%>%<cl_v%>: RegNounInfl ;
%<n%>%<cl_v%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-inan;

LEXICON NounJc-Aug-anim

%<n%>%<cl_j%>: RegNounInfl ;
%<n%>%<cl_j%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-anim;

LEXICON NounJc-Aug-inan

%<n%>%<cl_j%>: RegNounInfl ;
%<n%>%<cl_j%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-inan;

LEXICON NounJ2c-Aug-anim

%<n%>%<cl_j2%>: RegNounInfl ;
%<n%>%<cl_j2%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-anim;

LEXICON NounJ2c-Aug-inan

%<n%>%<cl_j2%>: RegNounInfl ;
%<n%>%<cl_j2%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-inan;

!LEXICON NounUc-Aug-anim

!%<n%>%<cl_u%>: RegNounInfl ;
!%<n%>%<cl_u%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-anim;

LEXICON NounUc-Aug-inan

%<n%>%<cl_u%>: RegNounInfl ;
%<n%>%<cl_u%>%<abs%>%<pl%>:  RegNounInfl-AbsPl-inan;





LEXICON RegNounUninfl
%<abs%>%<sg%>: # ;

LEXICON RegNounInfl

%<erg%>%<sg%>: RegNounInfl-Erg ;
%<erg%>%<pl%>:%>%{a%}ша # ;
%<gen%>%<sg%>:%>%{a%}н # ;
%<gen%>%<pl%>:и%{e%}н # ;
%<dat%>%<sg%>:%>%{a%}на # ;
%<dat%>%<pl%>:%>%{a%}шана # ;
%<all%>%<sg%>: RegNounInfl-All ;
%<all%>%<pl%>:%>%{a%}шка # ;
%<ins%>%<sg%>:%>%{a%}ца # ;
%<ins%>%<pl%>:%>%{a%}шца # ;
%<lat%>%<sg%>:%>%{a%}х # ;
%<lat%>%<pl%>:%>%{YY%}х # ;
%<cmp%>%<sg%>:%>%{a%}л # ;
%<cmp%>%<pl%>:%{YY%}л # ;
%<ine%>%<sg%>:%>%{a%}хь # ;
%<ine%>%<pl%>:%>%{a%}шкахь # ;
%<abl%>%<sg%>:%>%{a%}ра # ;
%<abl%>%<pl%>:%>%{a%}шкара # ;

LEXICON RegNounInfl-AbsPl-inan
:%>%{a%}ш # ;

LEXICON RegNounInfl-AbsPl-anim
:%>%{i%}й # ;

LEXICON RegNounInfl-Erg
:%>%{h%}о # ;

LEXICON RegNounInfl-All
:%>%{a%}%{г%}%{A1%} # ;

LEXICON PropNounUninfl
%<np%>%<abs%>%<sg%>: # ;

LEXICON PropNounFInfl
%<np%>%<fam%>%<abs%>%<sg%>: # ;
%<np%>%<fam%>%<abs%>%<pl%>:гӀар # ;


LEXICON Verb-IV

%<v%>%<iv%>: VerbInfl ;

LEXICON Verb-TV
%<v%>%<tv%>: VerbInfl ;

LEXICON VerbInfl
%<inf%>:%>а # ;
%<pres%>:%>у # ;
%<pres%>%<prog%>:%>уш # ;
%<pres%>%<pf%>:%>%{i1%}%{H%}а # ;
%<past%>%<rec%>%<wit%>:%>и # ;
%<past%>%<rem%>%<wit%>:%>ира # ;
%<past%>%<imp%>:%>ура # ;
%<past%>%<pf%>%<uwit%>:%>%{i1%}нера # ;
%<fut%>%<ptn%>:%>ур # ;




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          L E X I C O N                                  !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounRoot

кӀант:кӀант NounVc ; ! "boy"
кӀант:кӀент NounVc-Aug-anim; ! "boys"

йоӀ:йоӀ NounJc ; ! "girl"
йоӀ:мехкар NounJc-Aug-anim ; ! "girls"

малх:малх NounBc ; ! "sun"
малх:мелх NounBc-Aug-inan ; ! "suns"

Ӏаж:Ӏаж NounB2c ; ! "apple"
Ӏаж:Ӏеж NounB2c-Aug-inan ; ! "apples"

кор:кор NounDc ; ! "window"
кор:кор NounDc-Aug-inan ; ! "window"

гала:гала NounDc ; ! "noodle"
гала:гална NounDc-Aug-inan ; ! "noodle"

лом:лом NounDc ; ! "lion"
лом:лоьм NounDc-Aug-anim ; ! "lion"

хьаша:хьаша NounJ2c ; ! "guest"
хьаша:хьеш NounJ2c-Aug-anim ; ! "guest"

моз:моз NounDc ; ! "fly(noun)"
моз:моз NounDc-Aug-anim ; ! "fly(noun)"

ламаро:ламаро NounJ2c ; ! "mountainer"
ламаро:ламаро NounJ2c-Aug-anim ; ! "mountainer"

патар:патар NounDc ; ! "leaf"
патар:патар%{x%} NounDc-Aug-inan ; ! "leaf"

кол:кол NounJ2c ; ! "bush"
кол:коьл%{g%} NounJ2c-Aug-inan ; ! "bush"

ахгӀайре:ахгӀайре NounJ2c ; ! "peninsula"
ахгӀайре:ахгӀайре NounJ2c-Aug-inan ; ! "peninsula"

кулинарка:кулинарка NounJc ; ! "cookery"
кулинарка:кулинарк NounJc-Aug-inan ; ! "cookery"

школа:школа NounJc ; ! "school"
школа:школ NounJc-Aug-inan ; ! "school"

масса:масса NounJ2c ; ! "living creature"
масса:масс NounJ2c-Aug-anim ; ! "living creature"

тайпа:тайпа NounDc ; ! "sort, kind"
тайпа:тайпан NounDc-Aug-inan ; ! "sort, kind"

кан:кан NounDc ; ! "hair, wool"
кан:кан NounDc-Aug-inan ; ! "hair, wool"

бамба:бамба NounBc ; ! "cotton"
бамба:бамб NounBc-Aug-inan ; ! "cotton"

чолхе:чолхе NounJc ; ! "task, complicated task"
чолхе:чолхен NounJc-Aug-inan ; ! "task, complicated task"

накъосталла:накъосталла NounDc ; ! "help, fellowship"
накъосталла:накъосталл NounDc-Aug-inan ; ! "help, fellowship"

февраль:февраль NounB2c ; ! "February"
февраль:февраль NounB2c-Aug-inan ; ! "February"

сема:сема NounJc ; ! "access, axle"
сема:семн NounJc-Aug-inan ; ! "access, axle"

юьхк:юьхк NounJc ; ! "stump, block, core of an apple"
юьхк:юьхк NounJc ; ! "stump, block, core of an apple"

училище:училище NounDc ; ! "school"
училище:училище NounDc-Aug-inan ; ! "school"

актёр:актёр NounVc ; ! "actor"
актёр:актёр NounVc-Aug-inan ; ! "school"

музыка:музыка NounUc ; ! "music"
музыка:музык NounUc-Aug-inan ; ! "music"

! Irregular Plural (plural forms hard-coded because the inflection does not conform to any rule)

стаг:стаг NounJ2c ; ! "person"
стаг%<n%>%<cl_j2%>%<abs%>%<pl%>:нах # ; ! "people"


LEXICON PropNounRoot

Сатуев:Сатуев PropNounFInfl ; ! "Satujev"
Дала:Дала PropNounUninfl ; ! "God"

LEXICON AdjRoot

чолхе%<adj%>:чолхе # ; ! "complex"
сема%<adj%>:сема # ; !"alert, sharp"

LEXICON VerbRoot

мала:м%{A%}л Verb-TV ; ! "drink"

дада:д%{A%}д Verb-IV ; ! "run"

кхарза:кх%{A%}рз Verb-TV ; ! "fry"

зен:з%{E%}%{b%}%{h%} Verb-TV ; ! "examine"

лен:л%{E%}%{b%}%{h%} Verb-IV ; ! "talk"

эца:%{EE%}%{b%}ц Verb-TV ; ! "buy"

тоса:т%{O%}с Verb-IV ; ! "feel"

дохка:д%{O%}хк Verb-TV ; ! "sell"

дижа:д%{I%}%{b1%}ж Verb-IV ; ! "sleep"

хила:х%{I%}%{b1%}л Verb-IV ; ! "happen"

дотта:до%{b2%}тт Verb-TV ; ! "pour"

даккха:да%{b2%}ккх Verb-TV ; ! "receive"

деша:д%{E%}%{b1%}ш Verb-TV ; ! "read"

детта:д%{Y%}%{e%}тт Verb-TV ; ! "throw"

тоха:т%{O%}%{b2%}х Verb-TV ; ! "hit/strike"

дига:диг Verb-TV ; ! "lead"

дула:дул Verb-IV ; ! "inflate"

ала:%{AA%}л Verb-IV ; ! "say"

латта:ла%{b%}тт Verb-IV ; ! "grow,support"


LEXICON Conjunction

а%<conj%>: а # ; ! "and"


LEXICON ProNoun

иза%<prn%>%<pers%>%<p3%>%<abs%>%<sg%>: иза # ; ! "he/she/it"

уьш%<prn%>%<pers%>%<p3%>%<abs%>%<pl%>: уьш # ; ! "they"

и%<prn%>%<dem%>%<dst%>: и # ; ! "that"


! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://www.cis.upenn.edu/~cis639/docs/lexc.html
! http://wiki.apertium.org/wiki/Hfst
