Alphabet
 Ё Й Ц У К Е Н Г Ш Щ З Х Ъ Ӏ Ф Ы В А П Р О Л Д Ж Э Я Ч С М И Т Ь Б Ю
 ё й ц у к е н г ш щ з х ъ ӏ ф ы в а п р о л д ж э я ч с м и т ь б ю


 %{a%}:а %{a%}:0
 %{a1%}:а %{a1%}:0
 %{i%}:и %{i%}:0
 %{i1%}:и %{i1%}:0
 %{h%}:н %{h%}:0
 %{H%}:н %{H%}:л
 %{e%}:й %{e%}:0
 %{y%}:у %{y%}:0 %{y%}:а
 %{g%}:л %{g%}:0 %{g%}:н %{g%}:р ! the geminated place holder, more to come
 %{ee%}:е %{ee%}:0
 %{г%}:г %{г%}:0
 %{x%}:0

 %{b%}:ь %{b%}:0
 %{b1%}:ь %{b1%}:0
 %{b2%}:ь %{b2%}:0
 %{b3%}:ь %{b3%}:0
 ! %{b4%}:ь %{b4%}:0
 %{YY%}:и %{YY%}:я
 %{A%}:а %{A%}:е %{A%}:о
 %{A1%}:а %{A1%}:е
 %{A2%}:а %{A2%}:о
 %{E%}:о %{E%}:е
 %{EE%}:э %{EE%}:о
 %{O%}:о %{O%}:у
 %{I%}:и %{I%}:у
 %{Y%}:е %{Y%}:и
 ! %{AA%}:а %{AA%}:о


 %>:0  ;

Sets

Vow = А Я Э Е О Ё Ы И У Ю
      а я э е о ё ы и у ю ;

Cons =  Й Ц К Н Г Ш Щ З Х Ъ Ф В П Р Л Д Ж Ч С М Т Ь Б
        й ц к н г ш щ з х ъ ф в п р л д ж ч с м т ь б ;

Rules

"Spell relax for palochka"
a => _ ;


"Plurals"
%{a%}:0 <=> :Vow ( %>: ) _ ;
            %{x%}: %>: _ ;

"deletion of и after o"
%{i%}:0 <=> :о %>: _ й ;
            %{A%}: л _ ;
            %{E%}: ш _ ;



"Have a buffer consonant н before suffix -о"
%{h%}:н <=> :Vow ( %>: ) _ ;
            а _ а ;

"gemination"
%{g%}:Cy <=> :Cx ( %>: ) _ ;
      where Cx in ( л н р )
            Cy in ( л н р )
      matched ;


"allative case ending1"
%{A1%}:а <=> :Vow %>: %{a%}: %{г%}: _ ;

"allative case ending2"
%{A1%}:е <=> :Cons %>: %{a%}: %{г%}: _ ;

"Allative case 2-1"
%{г%}:0 <=> :Cons %>: %{a%}: _ %{A1%}: ;

"buffer a"
%{a%}:а <=> :Cons ( %>: ) _ :Cons ;
        except
            н ( %>: ) _ на ;

!"buffer a1"
!%{a1%}:0 <=> :Cons %>: _ %{г%}: %{A1%}: ;

"alternation of a to o/e"
%{A%}:Vy <=> _ :Cons ( :Cons ) %>: Vx ;
      where Vx in ( а у и )
            Vy in ( а о е )
      matched ;

"alternation of a to o/e 2"
%{A%}:е <=> _ :Cons %{i1%}: %{H%}: а ;
            _ :Cons %{i1%}: нера ;

"gemination h"
%{H%}:л <=> :л %>: %{i%}: _ ;

!@ л:л о:о ь:ь м:м >:0 {а}:0 {г}:0 {A1}:е

"alternation of o to y"
%{O%}:о <=> _ :Cons ( :Cons ) %>: а ;

"alternation of u to y"
%{I%}:у <=> _ :Cons ( :Cons ) %>: у ;

"insertion of b1"
%{b1%}:ь <=> _ :Cons ( :Cons ) %>: у ;

"insertion of b2"
%{b2%}:0 <=> _ :Cons ( :Cons ) %>: а ;

"insertion of b3"
%{b3%}:ь <=> _ :Cons ( :Cons ) %>: и ;

"alternation of e into и"     ! still not working perfectly cry
%{Y%}:е <=> _ :Cons ( :Cons ) %>: а ;
            _ :Cons ( :Cons ) %>: %{i1%}: нера ;

"alternation of e into и 2"
%{e%}:0 <=> _ :Cons ( :Cons ) %>: а ;
            _ :Cons ( :Cons ) %>: %{i1%}: нера ;

"insertion of и"
%{i1%}:и <=> :т %>: _ ;
             :х %>: _ ;

"alternation of e to o"
%{E%}:о <=> _ :Cons ( :Cons ) %>: у ;

"alternation of a to o"
%{A2%}:о <=> _ %{b3%}: :Cons ( :Cons ) ( :Cons ) %>: у ;




! Resources:
